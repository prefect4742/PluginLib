apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

buildscript {
    ext {
        versionMajor = 0
        versionMinor = 1
        versionPatch = 12

        androidx_version = '1.1.0-alpha02'
        androidx_appcompat_version = '1.1.0-alpha01'
        androidx_constraintlayout_version = '2.0.0-alpha3'
        kotlin_version = '1.3.11'
        kotlin_coroutines_version = '1.0.1'
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-rc03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.17'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28

        versionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName = "$versionMajor.$versionMinor.$versionPatch"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    // Avoid Kotlin docs error
    tasks.withType(Javadoc) {
        enabled = false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.preference:preference:$androidx_version"

    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
}

if (rootProject.file('release-bintray.gradle').exists()) {
    apply from: rootProject.file('release-bintray.gradle')
}
